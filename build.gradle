plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false
}

subprojects {
    repositories {
        mavenCentral()
    }
}

project(':java') {
    subprojects {
        apply plugin: 'application'
        apply plugin: 'java'
        dependencies {
            implementation "com.google.guava:guava:${guavaVersion}"
        }
        sourceSets {
            main {
                resources {
                    srcDirs = ['.']
                    include "README.md"
                    include "CHANGELOG.md"
                    include "LICENSE"
                }
            }
        }
        buildDir = "${project.rootDir}/dist/java/gradle/${projectDir.name}"
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(javaToolchainVersion)
            }
        }
        run {
            standardInput = System.in
        }
        defaultTasks "java-${projectDir.name}"
    }
    tasks.register('build') {
        group = 'Application (Java)'
        description = 'Builds all Java targets'
    }
    subprojects.each { subproject ->
        tasks.named("build").configure {
            dependsOn ":java:${subproject.name}:build"
        }
    }
}

project(':kotlin') {
    subprojects {
        apply plugin: 'application'
        apply plugin: 'kotlin'
        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        }
        sourceSets {
            main {
                resources {
                    srcDirs = ['.']
                    include "README.md"
                    include "CHANGELOG.md"
                    include "LICENSE"
                }
            }
        }
        buildDir = "${project.rootDir}/dist/kotlin/gradle/${projectDir.name}"
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(kotlinJvmToolchainVersion)
            }
        }
        run {
            standardInput = System.in
        }
        defaultTasks "kotlin-${projectDir.name}"
    }
    tasks.register('build') {
        group = 'Application (Kotlin)'
        description = 'Builds all Kotlin targets'
    }
    subprojects.each { subproject ->
        tasks.named("build").configure {
            dependsOn ":kotlin:${subproject.name}:build"
        }
    }
}
