plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.20' apply false
}

ext {
    guavaVersion = '32.1.1-jre'
    kotlinVersion = '1.9.20'
    javaToolchainVersion = 21
    kotlinJvmToolchainVersion = 21
}

subprojects {
    repositories {
        mavenCentral()
    }
}

project(':java') {
    subprojects {
        apply plugin: 'java'
        dependencies {
            implementation "com.google.guava:guava:${guavaVersion}"
        }
        sourceSets {
            main {
                java {
                    srcDirs = ['.']
                }
            }
        }
        buildDir = "../../bin/java/${projectDir.name}"
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(javaToolchainVersion)
            }
        }
        defaultTasks "java-${projectDir.name}"
    }
    tasks.register('build') {
        group = 'Application (Java)'
        description = 'Builds all Java targets'
    }
    subprojects.each { subproject ->
        tasks.named("build").configure {
            dependsOn ":java:${subproject.name}:build"
        }
    }
}

project(':kotlin') {
    subprojects {
        apply plugin: 'kotlin'
        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        }
        sourceSets {
            main {
                kotlin {
                    srcDirs = ['.']
                }
            }
        }
        buildDir = "../../bin/kotlin/${projectDir.name}"
        kotlin {
            jvmToolchain(kotlinJvmToolchainVersion)
        }
        defaultTasks "kotlin-${projectDir.name}"
    }
    tasks.register('build') {
        group = 'Application (Kotlin)'
        description = 'Builds all Kotlin targets'
    }
    subprojects.each { subproject ->
        tasks.named("build").configure {
            dependsOn ":kotlin:${subproject.name}:build"
        }
    }
}
