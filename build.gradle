plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.10' apply false
}

subprojects {
    repositories {
        mavenCentral()
    }
}

project(':java') {
    subprojects {
        apply plugin: 'java'
        dependencies {
            implementation "com.google.guava:guava:${guavaVersion}"
        }
        sourceSets {
            main {
                java {
                    srcDirs = ['src']
                }
            }
        }
        buildDir = "${project.rootDir}/dist/java/${projectDir.name}"
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(javaToolchainVersion)
            }
        }
        defaultTasks "java-${projectDir.name}"
    }
    tasks.register('build') {
        group = 'Application (Java)'
        description = 'Builds all Java targets'
    }
    subprojects.each { subproject ->
        tasks.named("build").configure {
            dependsOn ":java:${subproject.name}:build"
        }
    }
}

project(':kotlin') {
    subprojects {
        apply plugin: 'kotlin'
        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        }
        sourceSets {
            main {
                kotlin {
                    srcDirs = ['src']
                }
            }
        }
        buildDir = "${project.rootDir}/dist/kotlin/${projectDir.name}"
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(kotlinJvmToolchainVersion)
            }
        }
        defaultTasks "kotlin-${projectDir.name}"
    }
    tasks.register('build') {
        group = 'Application (Kotlin)'
        description = 'Builds all Kotlin targets'
    }
    subprojects.each { subproject ->
        tasks.named("build").configure {
            dependsOn ":kotlin:${subproject.name}:build"
        }
    }
}
